// DESERT SCENARIO 3 Mission 2 The Great Gold Rush


//PAD Defs
PAD ExitH		10	GreenHalftrack1 > 0
PAD ExitS		10	GreenSarge > 0
PAD ExitTan		10	TAN > 0
PAD BankHole1	74	GreenRifle1 > 0
PAD BankHole2	74	GreenRifle2 > 0
PAD Trip1		50	GreenSarge > 0
PAD Trip2		51 	GreenSarge > 0

PAD Path20		20	TanTank5 > 0
PAD Path21		21	TanTank5 > 0
PAD Path22		22	TanTank5 > 0
PAD Path23		23	TanTank5 > 0
PAD Path24		24	TanTank2 > 0
PAD Path25		25	TanTank2 > 0
PAD Path26		26	TanTank2 > 0
PAD Path27		27	TanTank2 > 0
PAD Path28		28	TanTank2 > 0
PAD Path29		29	TanTank6 > 0
PAD Path30		30	TanTank6 > 0
PAD Path31		31	TanTank6 > 0
PAD Path32		32	TanTank6 > 0
PAD Path33		33	TanTank6 > 0

PAD TankTrip2	2	TanTank2 > 0
PAD TankTrip5	5	TanTank5 > 0
PAD TankTrip6	6	TanTank6 > 0

//ENDGAMES
IF killed_GreenHalftrack1 THEN
	PLAYSTREAM DESERT3_1LOSE,	//using same as previous mission
	SHOWBMP "Desert3_1Lose.bmp",	//using same as previous mission
	INVULNERABLESARGE,
	TRIGGERDELAY 1 tanwins


IF ALLOF killed_GreenRifle1 killed_GreenRifle2 THEN
	PLAYSTREAM DESERT3_1LOSE,	//using same as previous mission
	SHOWBMP "Desert3_1Lose.bmp",	//using same as previous mission
	INVULNERABLESARGE,
	TRIGGERDELAY 1 tanwins

IF ALLOF killed_GreenSpecial101 killed_GreenSpecial102 THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 tanwins

IF ALLOF killed_GreenRifle1 killed_GreenSpecial102 THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 tanwins

IF ALLOF killed_GreenSpecial101 killed_GreenRifle2 THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 tanwins



IF killed_GreenSarge THEN
	TRIGGERDELAY 3 tanwins

IF ALLOF on_ExitH on_ExitS THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 greenwins

//oops
IF ALLOF killed_Arch1 killed_Arch2 THEN
	SHOWBMP "D31bad.bmp",
	PLAYSTREAM D31BAD
//	INVULNERABLESARGE,
//	TRIGGERDELAY 1 tanwins

IF killed_Arch3 THEN
	SHOWBMP "D31bad.bmp",
	PLAYSTREAM D31BAD
//	INVULNERABLESARGE,
//	TRIGGERDELAY 1 tanwins


//INIT
IF startup THEN
	VULNERABLESARGE,
	SETAICONTROL GreenHalftrack1,
	REMOVEOBJECT Lure1,
	TRIGGER GoGoTankMan2



//Game Events

IF killed_Bank THEN
	TRIGGERDELAY 1 GoBank,
	TRIGGERDELAY 3 TankMagnet

IF GoBank THEN
	PLAYSTREAM LOAD_TRUCKS_QUICK,
	SETAICONTROL GreenHalftrack1,
	ORDER GreenHalftrack1 GOTO (2650, 2815) INMODE IGNORE THEN TRIGGER Turn

IF Turn THEN
	ORDER GreenHalftrack1 GOTO (2630, 2910) INMODE DEFEND THEN TRIGGER Delay
IF Delay THEN
	TRIGGERDELAY 3 GetOut
IF GetOut THEN
	SETAICONTROL GreenRifle7,
	ORDER GreenRifle7 GOTO (2050, 2780) INMODE ATTACK THEN ATMODE DEFEND,
	SETUSERCONTROL GreenRifle7,
	SETAICONTROL GreenRifle1,
	ORDER GreenRifle1 GOTO BankHole1 INMODE IGNORE THEN TRIGGER AtBank1,
	TRIGGERDELAY 2 Unit2
IF Unit2 THEN
	SETAICONTROL GreenRifle2,
	ORDER GreenRifle2 GOTO BankHole2 INMODE IGNORE THEN TRIGGER AtBank2

IF AtBank1 THEN
	UNDEPLOY GreenRifle1,
	TRIGGERDELAY 1 Load1
IF Load1 THEN
	DEPLOY GreenSpecial101 BankHole1 ATMODE IGNORE,
	SETAICONTROL GreenSpecial101,
	ORDER GreenSpecial101 GOTO GreenHalftrack1 INMODE IGNORE THEN TRIGGER AtTruck1
IF AtTruck1 THEN
	UNDEPLOY GreenSpecial101,
	TRIGGER Counter,
	TRIGGERDELAY 1 NoLoad1
IF NoLoad1 BUTNOT Stop THEN
	DEPLOY GreenRifle1 GreenHalftrack1 ATMODE IGNORE,
	SETAICONTROL GreenRifle1,
	ORDER GreenRifle1 GOTO BankHole1 INMODE RETURNFIRE THEN TRIGGER AtBank1

//unit number 2wo
IF AtBank2 THEN
	UNDEPLOY GreenRifle2,
	TRIGGERDELAY 2 Load2
IF Load2 THEN
	DEPLOY GreenSpecial102 BankHole2 ATMODE IGNORE,
	SETAICONTROL GreenSpecial102,
	ORDER GreenSpecial102 GOTO GreenHalftrack1 INMODE IGNORE THEN TRIGGER AtTruck2
IF AtTruck2 THEN
	UNDEPLOY GreenSpecial102,
	TRIGGER Counter,
	TRIGGERDELAY 2 NoLoad2
IF NoLoad2 BUTNOT Stop THEN
	DEPLOY GreenRifle2 GreenHalftrack1 ATMODE IGNORE,
	SETAICONTROL GreenRifle2,
	ORDER GreenRifle2 GOTO BankHole2 INMODE RETURNFIRE THEN TRIGGER AtBank2

//how many times should I enter the truck?
IF REPEAT 30 OF Counter THEN
	TRIGGER Stop,
	TRIGGER On,
	SETAICONTROL GreenRifle7,
	ORDER GreenRifle7 FOLLOW GreenHalftrack1 INMODE IGNORE THEN TRIGGER 7InTruck

IF 7InTruck THEN
	SHOWBMP "Ds3m2B.BMP",
	PLAYSTREAM DESERT3_2M,
	TRIGGERDELAY 5 Lego
	

IF ALLOF Stop killed_GreenRifle7 THEN
	TRIGGER 7InTruck

IF Lego THEN
	PLAYSTREAM GET_OUT_OF_TOWN,
	TRIGGER GoHome

IF GoHome THEN
	ORDER GreenHalftrack1 GOTO (509, 3760) SLOW INMODE DEFEND THEN TRIGGER Repeat

IF Repeat THEN
	TRIGGERDELAY 1 GoHome



IF GoGoTankMan2 THEN
	ORDER TanRifle10 FOLLOW TanTank5 INMODE DEFEND,
	ORDER TanRifle11 FOLLOW TanTank6 INMODE DEFEND,
	ORDER TanRifle13 FOLLOW TanTank2 INMODE DEFEND,
	TRIGGER GoGoTank2,
	TRIGGER GoGoTank5,
	TRIGGER GoGoTank6

//TankPatrols
IF GoGoTank2 BUTNOT SouthHammerParty THEN SEQUENTIAL
	ORDER TanTank2 GOTO Path24 SLOW INMODE ATTACK THEN TRIGGER GoGoTank2,
	ORDER TanTank2 GOTO Path25 SLOW INMODE ATTACK THEN TRIGGER GoGoTank2,
	ORDER TanTank2 GOTO Path26 SLOW INMODE ATTACK THEN TRIGGER GoGoTank2,
	ORDER TanTank2 GOTO Path27 SLOW INMODE ATTACK THEN TRIGGER GoGoTank2,
	ORDER TanTank2 GOTO Path28 SLOW INMODE ATTACK THEN TRIGGER GoGoTank2
IF on_TankTrip2 AFTER off_TankTrip2 THEN
	TRIGGERDELAY 5 GoGoTank2

IF GoGoTank5 BUTNOT SouthHammerParty THEN SEQUENTIAL
	ORDER TanTank5 GOTO Path20 SLOW INMODE ATTACK THEN TRIGGER GoGoTank5,
	ORDER TanTank5 GOTO Path21 SLOW INMODE ATTACK THEN TRIGGER GoGoTank5,
	ORDER TanTank5 GOTO Path22 SLOW INMODE ATTACK THEN TRIGGER GoGoTank5,
	ORDER TanTank5 GOTO Path23 SLOW INMODE ATTACK THEN TRIGGER GoGoTank5
IF on_TankTrip5 AFTER off_TankTrip5 THEN
	TRIGGERDELAY 5 GoGoTank5


IF GoGoTank6 BUTNOT NorthHammerParty THEN SEQUENTIAL
	ORDER TanTank6 GOTO Path29 SLOW INMODE ATTACK THEN TRIGGER GoGoTank6,
	ORDER TanTank6 GOTO Path30 SLOW INMODE ATTACK THEN TRIGGER GoGoTank6,
	ORDER TanTank6 GOTO Path31 SLOW INMODE ATTACK THEN TRIGGER GoGoTank6,
	ORDER TanTank6 GOTO Path32 SLOW INMODE ATTACK THEN TRIGGER GoGoTank6,
	ORDER TanTank6 GOTO Path33 SLOW INMODE ATTACK THEN TRIGGER GoGoTank6
IF on_TankTrip6 AFTER off_TankTrip6 THEN
	TRIGGERDELAY 5 GoGoTank6




IF TankMagnet THEN
	TRIGGER SetSouth1,
	TRIGGER NorthHammerParty,
	TRIGGERDELAY 5 OutTank101

IF OutTank101 THEN
	DEPLOY TanTank101 ATMODE ATTACK,
	ORDER TanTank101 GOTO (480, 3740) SLOW INMODE ATTACK THEN TRIGGER Spot2
IF Spot2 THEN
	ORDER TanTank101 GOTO (890, 3640) SLOW INMODE ATTACK THEN TRIGGER GoGoTank101

IF REPEAT 1 OF GoGoTank101 THEN
	TRIGGERDELAY 3 OutRifle101,
	TRIGGERDELAY 10 OutRifle102

IF GoGoTank101 THEN
	ORDER TanTank101 GOTO GreenHalftrack1 SLOW INMODE ATTACK THEN TRIGGER UberTank

IF UberTank THEN
	TRIGGERDELAY 5 GoGoTank101

IF OutRifle101 THEN
	DEPLOY TanRifle101 ATMODE ATTACK,
	ORDER TanRifle101 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK

IF OutRifle102 THEN
	DEPLOY TanRifle102 ATMODE ATTACK,
	ORDER TanRifle102 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK

IF killed_TanRifle101 BUTNOT On THEN
	TRIGGERDELAY 3 ResTanRifle101

IF ResTanRifle101 THEN
	RESURRECT TanRifle101 (150, 3530),
	ORDER TanRifle101 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK

IF killed_TanRifle102 BUTNOT On THEN
	TRIGGERDELAY 5 ResTanRifle102
IF ResTanRifle102 THEN
	RESURRECT TanRifle102 (150, 3610),
	ORDER TanRifle102 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK

IF SetSouth1 THEN
	ORDER TanTank3 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK,
	TRIGGERDELAY 5 SouthHammerParty

IF SouthHammerParty THEN
	TRIGGERDELAY 0.1 southTank1,
	TRIGGERDELAY 0.2 southTank2,
	TRIGGERDELAY 0.3 southTank3,
	TRIGGERDELAY 0.4 southTank5,
	TRIGGERDELAY 0.5 southTank8,
	TRIGGERDELAY 0.6 south18,
	TRIGGERDELAY 0.7 south2,
	ORDER TanRifle3 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK,
	TRIGGERDELAY 10 SouthHammerParty

IF southTank1 THEN ORDER TanTank1 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF southTank2 THEN ORDER TanTank2 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF southTank3 THEN ORDER TanTank3 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF southTank5 THEN ORDER TanTank5 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF southTank8 THEN ORDER TanTank8 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF south18 THEN ORDER TanRifle18 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF south2 THEN ORDER TanRifle2 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK

IF NorthHammerParty THEN
	TRIGGERDELAY 0.1 southTank1,
	TRIGGERDELAY 0.2 southTank2,
	TRIGGERDELAY 0.3 northTank6,
	TRIGGERDELAY 0.4 southTank5,
	TRIGGERDELAY 0.5 southTank8,
	TRIGGERDELAY 0.6 northTank7,
	TRIGGERDELAY 0.7 north3,
	TRIGGERDELAY 0.8 north1,
	TRIGGERDELAY 0.8 northGren,
	TRIGGERDELAY 0.1 north15,
	TRIGGERDELAY 0.1 north8,
	ORDER TanRifle16 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK,
	ORDER TanRifle17 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK,
	TRIGGERDELAY 12 NorthHammerParty

IF northTank6 THEN ORDER TanTank6 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF northTank7 THEN ORDER TanTank7 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF north3 THEN 	ORDER TanRifle3 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF north1 THEN 	ORDER TanRifle1 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF northGren THEN ORDER TanGrenadier1 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF north8 THEN ORDER TanRifle8 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK
IF north15 THEN ORDER TanRifle15 GOTO GreenHalftrack1 INMODE ATTACK THEN ATMODE ATTACK

//go grab that tank man!
IF REPEAT 1 OF on_Trip1 THEN
	TRIGGER GoTR8
IF GoTR8 BUTNOT killed_TanTank1 THEN
	ORDER TanRifle8 GOTO TanTank1 INMODE IGNORE THEN TRIGGER GetIn
IF GetIn THEN
	UNDEPLOY TanRifle8,
	TRIGGERDELAY 1.2 Ignition
IF Ignition THEN
	ORDER TanTank1 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK
IF ALLOF GoTR8 killed_TanTank1 THEN
	ORDER TanRifle8 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK


IF REPEAT 1 OF on_Trip2 THEN
	TRIGGER GoTR15
IF GoTR15 BUTNOT killed_TanTank8 THEN
	ORDER TanRifle15 GOTO TanTank8 INMODE IGNORE THEN TRIGGER GetIn2
IF GetIn2 THEN
	UNDEPLOY TanRifle15,
	TRIGGERDELAY 1.5 Ignition2
IF Ignition2 THEN
	ORDER TanTank8 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK
IF ALLOF GoTR15 killed_TanTank8 THEN
	ORDER TanRifle15 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK


//Itemdrops
IF killed_MagiCrate5 THEN
	REMOVEOBJECT Meds1,
	PLACEOBJECT Meds1 MagiCrate5
IF killed_MagiCrate2 THEN
	REMOVEOBJECT Bazooka2,
	PLACEOBJECT Bazooka2 MagiCrate2
IF killed_MagiCrate3 THEN
	REMOVEOBJECT Meds3,
	PLACEOBJECT Meds3 MagiCrate3
IF killed_MagiCrate4 THEN
	REMOVEOBJECT Meds4,
	PLACEOBJECT Meds4 MagiCrate4
IF killed_HidingCrate3 THEN
	REMOVEOBJECT Flam7,
	PLACEOBJECT Flam7 HidingCrate3
IF killed_SecretCrate1 THEN
	REMOVEOBJECT MortarMe,
	PLACEOBJECT MortarMe SecretCrate1
IF killed_ReconCrate11 THEN
	REMOVEOBJECT Recon11,
	PLACEOBJECT Recon11 ReconCrate11
IF killed_ReconCrate12 THEN
	REMOVEOBJECT Recon12,
	PLACEOBJECT Recon12 ReconCrate12

